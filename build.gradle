apply plugin: 'java'
apply plugin: 'application'
apply plugin: 'jacoco'
apply plugin: 'pmd'
apply plugin: 'findbugs'

repositories {
    mavenCentral()
}

dependencies {
    compile group: 'com.google.guava', name: 'guava', version: '18.0'
    compile group: 'com.google.inject', name: 'guice', version: '4.0'
    compile group: 'org.python', name: 'jython', version: '2.7.0'
    compile group: 'org.bytedeco.javacpp-presets', name: 'opencv', version: '3.0.0-1.0'
    compile group: 'org.bytedeco.javacpp-presets', name: 'opencv', version: '3.0.0-1.0', classifier: "linux-x86"
    compile group: 'org.bytedeco.javacpp-presets', name: 'opencv', version: '3.0.0-1.0', classifier: "linux-x86_64"
    compile group: 'org.bytedeco.javacpp-presets', name: 'opencv', version: '3.0.0-1.0', classifier: "macosx-x86_64"
    compile group: 'org.bytedeco.javacpp-presets', name: 'opencv', version: '3.0.0-1.0', classifier: "windows-x86_64"
    compile group: 'org.bytedeco.javacpp-presets', name: 'opencv', version: '3.0.0-1.0', classifier: "windows-x86"
    testCompile group: 'junit', name: 'junit', version: '4.11'
}

jacocoTestReport {
    reports {
        xml.enabled = true
        html.enabled = true
    }
}
check.dependsOn jacocoTestReport

findbugs {
    ignoreFailures = true
    toolVersion = "3.0.1"
    sourceSets = [sourceSets.main]
    reportsDir = file("$project.buildDir/reports/findbugs")
    effort = "max"
}
tasks.withType(FindBugs) {
    reports {
        xml.enabled = false
        html.enabled = true
    }
}

pmd {
    toolVersion = '5.3.3'
    // This will be supported in gradle 2.7
    //rulePriority = 3
    ignoreFailures = false
    consoleOutput = true
    sourceSets = [sourceSets.main, sourceSets.test]
    reportsDir = file("$project.buildDir/reports/pmd")
    ruleSetFiles = files("config/pmd/RuleSet.xml")
    ruleSets = [] //http://sourceforge.net/p/pmd/discussion/188193/thread/6e9c6017/
}


allprojects {
    tasks.withType(Javadoc) {
        options.addStringOption('Xdoclint:all,-html', '-quiet')
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.6'
}

sourceCompatibility = 1.8
version = '0.1'
mainClassName = "edu.wpi.grip.Main"
