plugins { // Must be first
    id 'java' // or 'groovy' Must be explicitly applied
    id 'com.github.johnrengelman.shadow' version '1.2.2'
}

apply plugin: 'idea'
apply plugin: 'eclipse'
apply plugin: 'jacoco'

allprojects {
    apply plugin: 'java'
    apply plugin: 'application'

    repositories {
        mavenCentral()
        jcenter()
    }

    dependencies {
        compile group: 'com.google.guava', name: 'guava', version: '18.0'
        compile group: 'com.google.inject', name: 'guice', version: '4.0'
        compile group: 'org.python', name: 'jython', version: '2.7.0'
        compile group: 'com.thoughtworks.xstream', name: 'xstream', version: '1.4.8'
        compile group: 'org.bytedeco', name: 'javacv', version: '1.1'
        compile group: 'org.bytedeco.javacpp-presets', name: 'opencv', version: '3.0.0-1.1'
        compile group: 'org.bytedeco.javacpp-presets', name: 'opencv', version: '3.0.0-1.1', classifier: 'linux-x86'
        compile group: 'org.bytedeco.javacpp-presets', name: 'opencv', version: '3.0.0-1.1', classifier: 'linux-x86_64'
        compile group: 'org.bytedeco.javacpp-presets', name: 'opencv', version: '3.0.0-1.1', classifier: 'macosx-x86_64'
        compile group: 'org.bytedeco.javacpp-presets', name: 'opencv', version: '3.0.0-1.1', classifier: 'windows-x86_64'
        compile group: 'org.bytedeco.javacpp-presets', name: 'opencv', version: '3.0.0-1.1', classifier: 'windows-x86'
        testCompile group: 'org.hamcrest', name: 'hamcrest-all', version: '1.3'
        testCompile group: 'junit', name: 'junit', version: '4.12'
    }

    tasks.withType(Javadoc) {
        source compileJava.source
        options.addStringOption('Xdoclint:all,-html', '-quiet')
    }
}

project(':core') {
    apply plugin: "com.github.johnrengelman.shadow"

    mainClassName = 'edu.wpi.grip.Main'
    version = '0.1.0'

    jar {
        manifest {
            attributes 'Implementation-Title': 'GRIP CORE', 'Implementation-Version': version, 'Main-Class': mainClassName
        }
    }


    sourceSets {
        generated {
            java {
                /* Note: When referencing this it becomes `srcDirs` */
                srcDir 'src/generated/java'
            }
        }
    }

    task generateCodeFromSource(type: CodeGenerator) {
        description 'Generates the Operation Wrappers for the OpenCV methods.\n To skip run with parm `-PskipGenerate`'
        dest 'core/src/generated/java'
        removeExisting true
    }
    generateCodeFromSource.onlyIf { !project.hasProperty('skipGenerate') }
    compileJava.dependsOn generateCodeFromSource

    compileJava.source sourceSets.generated.java, sourceSets.main.java

    // IDE setup

    eclipse.classpath {
        file.whenMerged { cp ->
            sourceSets.generated.java.srcDirs.forEach { srcDir ->
                def source_folder = new org.gradle.plugins.ide.eclipse.model.SourceFolder(srcDir.getAbsolutePath(), "build/classes/merged")
                if (cp.entries.find() { it.path == source_folder.path } == null)
                    cp.entries.add(source_folder)
            }
        }
    }

    idea.module {
        sourceDirs += sourceSets.generated.java.srcDirs
    }
    // End IDE setup

}

project(':ui') {
    mainClassName = 'edu.wpi.grip.MainApplication'
    version = '0.1.0'

    apply from: 'http://dl.bintray.com/shemnon/javafx-gradle/8.1.1/javafx.plugin'

    dependencies {
        compile project(':core')
        compile group: 'org.controlsfx', name: 'controlsfx', version: '8.40.10'
        compile group: 'org.apache.commons', name: 'commons-lang3', version: '3.4'
        testCompile files(project(':core').sourceSets.test.output.classesDir)
        testCompile group: 'org.testfx', name: 'testfx-core', version: '4.0.+'
        testCompile group: 'org.testfx', name: 'testfx-junit', version: '4.0.+'
    }

    evaluationDependsOn(':core')
    compileTestJava.dependsOn tasks.getByPath(':core:testClasses')

    javafx {
        appID = 'GRIP'
        appName = 'GRIP'
        mainClass = mainClassName
    }
}

jacocoTestReport {
    reports {
        xml.enabled = true
        html.enabled = true
    }
}
check.dependsOn jacocoTestReport

task wrapper(type: Wrapper) {
    gradleVersion = '2.6'
}


