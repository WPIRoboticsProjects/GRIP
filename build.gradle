apply plugin: 'java'
apply plugin: 'application'
apply plugin: 'jacoco'
apply from: 'javafx.plugin'

sourceSets{
    generated{
        java{
            /* Note: When referencing this it becomes `srcDirs` */
            srcDir 'src/generated/java'
        }
    }
}


repositories {
    mavenCentral()
}

dependencies {
    compile group: 'org.controlsfx', name: 'controlsfx', version: '8.40.10'
    compile group: 'com.google.guava', name: 'guava', version: '18.0'
    compile group: 'com.google.inject', name: 'guice', version: '4.0'
    compile group: 'org.python', name: 'jython', version: '2.7.0'
    compile group: 'com.thoughtworks.xstream', name: 'xstream', version: '1.4.8'
    compile group: 'org.bytedeco', name: 'javacv', version: '1.0'
    compile group: 'org.bytedeco.javacpp-presets', name: 'opencv', version: '3.0.0-1.0'
    compile group: 'org.bytedeco.javacpp-presets', name: 'opencv', version: '3.0.0-1.0', classifier: "linux-x86"
    compile group: 'org.bytedeco.javacpp-presets', name: 'opencv', version: '3.0.0-1.0', classifier: "linux-x86_64"
    compile group: 'org.bytedeco.javacpp-presets', name: 'opencv', version: '3.0.0-1.0', classifier: "macosx-x86_64"
    compile group: 'org.bytedeco.javacpp-presets', name: 'opencv', version: '3.0.0-1.0', classifier: "windows-x86_64"
    compile group: 'org.bytedeco.javacpp-presets', name: 'opencv', version: '3.0.0-1.0', classifier: "windows-x86"
    testCompile group: 'junit', name: 'junit', version: '4.11'
}

compileJava.source sourceSets.generated.java, sourceSets.main.java

task generateCodeFromSource( type: CodeGenerator ){
    description 'Generates the Operation Wrappers for the OpenCV methods.\n To skip run with parm `-PskipGenerate`'
    dest sourceSets.generated.java.srcDirs
    removeExisting true
}
generateCodeFromSource.onlyIf { !project.hasProperty('skipGenerate') }
compileJava.dependsOn generateCodeFromSource

jacocoTestReport {
    reports {
        xml.enabled = true
        html.enabled = true
    }
}
check.dependsOn jacocoTestReport

allprojects {
    tasks.withType(Javadoc) {
        source compileJava.source
        options.addStringOption('Xdoclint:all,-html', '-quiet')
    }
}


task wrapper(type: Wrapper) {
    gradleVersion = '2.6'
}

sourceCompatibility = 1.8
version = '0.1'
mainClassName = "edu.wpi.grip.Main"
