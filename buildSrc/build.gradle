apply plugin: 'groovy'
apply plugin: 'project-report'

repositories {
    mavenCentral()
}

configurations {
    source
}

dependencies {
    compile gradleApi()
    compile localGroovy()
    compile group: 'com.google.guava', name: 'guava', version: '18.0'
    compile group: 'com.github.javaparser', name: 'javaparser-core', version: '2.2.2'
    source  group: 'org.bytedeco.javacpp-presets', name: 'opencv', version: '3.0.0-1.1', classifier: 'sources'
    testCompile group: 'junit', name: 'junit', version: '4.11'
}

task extractSources(type: Copy) {
    from { // use of closure defers evaluation until execution time
        configurations.source.collect { zipTree(it) }
    }
    include '**/*.java'
    exclude '**/META-INF/**'
    //Otherwise Intellj won't let the files be used as resources
    rename { filename ->
         filename.replace 'java', 'txt'

    }
    into "$projectDir/src/main/resources"
}

compileGroovy.dependsOn extractSources

task runScript (dependsOn: 'classes', type: JavaExec) {
    main = 'Main'
    classpath = sourceSets.main.runtimeClasspath
}

sourceCompatibility = 1.8
version = '0.1'